
Room_Key k_key "my key" kaitlyn
 class Britney_Object Ownable_Object
 with
  name 'room' 'key',
  article "my",
  owner kaitlyn,
  key_to_room room_402
 has proper;

Top yellow_shirt "yellow t-shirt" kaitlyn
 with
  name 'yellow' 'shirt' 'top' 't-shirt',
  description "It's my favourite yellow shirt.",
  article "my",
  owner kaitlyn
 has worn;

Pants skinny_jeans "skinny jeans" kaitlyn
 with
  name 'skinny' 'jeans' 'pants',
  description "They're skinny jeans.",
  article "my",
 has worn pluralname;

! Clothes that are initially hidden
Lingerie k_bra "bra" thedark
 with
  name 'my' 'bra',
  description "I love how my boobs look in this bra.",
  article "my",
 has ~worn;

! Might expect 'has talkable' for a phone, but this phone is basically a teleporter ;-)
Disappearing_Object smartphone "smartphone" kaitlyn
 with
  owner kaitlyn,
  name 'phone' 'smartphone' 'cell' 'cellular' 'mobile' 'screen' 'display',
  initial "~Hey, there's my smartphone!~",
  description [;
   if (self.mode ~= MODE_OFF) <Examine screen>;
   else print "I love my smartphone, it's got a full HD screen and, below the screen, is the Home button.^";
   if (FlagOn(SMARTPHONE_LED_BLINKING)) print_ret "^Hey, the notification light is blinking!";
   rtrue;
  ],
  invent "my smartphone", ! So inventory doesn't list the screen, buttons, icons, etc
  article "my",
  mode MODE_OFF,
  before [;
   Push, Touch:
    "You'll have to be more specific.";
   Drop:
    ! If telephone hasnt general, smartphone is being used
    if ((OnPhone()) && (telephone hasnt general)) "Don't be rude, you need to hang up first.";
  ],
  change_mode [new_mode o;
   ! Switching out of Text mode, clear existing conversation
   self.mode = new_mode;
   if (self.mode ~= MODE_OFF) self.interact();
   if (self.mode == MODE_HOME) objectloop (o ofclass Icon) move o to screen;
   else objectloop (o ofclass Icon) remove o;
   if (self.mode == MODE_CONTACTS) move contact_britney to screen;
   else remove contact_britney;
   if (self.mode == MODE_PHOTOS) {
    current_photo = MAX_PHOTOGRAPHS - Photograph.remaining();
   }
  ],
  interact [;
   self.move_counter = SMARTPHONE_TIMEOUT;
   StartDaemon(self);
  ],
  move_counter,
  daemon [;
   if (call_person == nothing) {
    self.move_counter--;
    if (self.move_counter == 0) {
     self.change_mode(MODE_OFF);
     StopDaemon(self);
     ! This should be unobtrusive, it's not like the doors swinging shut
     !print "^My smartphone times out and turns off.^";
    }
   }
  ]
 has container transparent;

Object screen "screen" smartphone
 with
  description [ o;
   switch (smartphone.mode) {
    MODE_OFF: "It's off.";
    MODE_HOME: "The screen's got four icons, labelled Contacts, Messages, Camera and Photos.";
    ! Contacts is only for outgoing calls to Britney
    MODE_CONTACTS:
     if (call_person == britney && telephone hasnt general) "It has a picture of Britney and a Hang Up button.";
     "Contacts:^ Britney";
    ! Text mode is only to receive the ransom message from anonymous
    MODE_MESSAGES:
     ClearFlag(SMARTPHONE_LED_BLINKING);
     SetFlag(PLAYER_KNOWS_ABOUT_FLUFFY);
     print_ret (preformatted)"Message from Anonymous:^We have your dog. If you want it back, send us a topless picture of yourself. You have one hour.";
    MODE_CAMERA: "Ready to take a photo.";
    MODE_PHOTOS:
     objectloop (o ofclass Photograph) if (o.number == current_photo) <<Examine o>>;
   }
  ],
  before [;
   Push, Touch: "You'll have to be more specific.";
   Take: "That would be vandalism and you're not that kind of person, are you?";
  ]
 has container transparent;

Object home_button "home" smartphone
 with
  name 'home',
  before [;
   Push, Touch:
    if (smartphone notin player) move smartphone to player;
    switch (smartphone.mode) {
     MODE_OFF: print "I pressed the Home button.^";
     MODE_HOME: print "Nothing happens.^";
     default: print "Back to home screen.^";
    }
    smartphone.change_mode(MODE_HOME);
    rtrue;
  ]
 has static;

! The icons have to be moved to the screen when in MODE_HOME, otherwise removed so you can't interact with them
Icon contacts_icon "contacts"
 with
  mode MODE_CONTACTS,
  name 'Contacts';

Icon messages_icon "messages"
 with
  mode MODE_MESSAGES,
  name 'Messages';

Icon camera_icon "camera"
 with
  mode MODE_CAMERA,
  name 'Camera';

Icon photos_icon "photos"
 with
  mode MODE_PHOTOS,
  name 'Photos';

Object contact_britney
 with
  name 'Britney',
  before [;
   Push, Touch:
    if (OnPhone()) "I'm currently on the phone with ", (name)call_person, ".";
    if ((smartphone notin actor) || (smartphone.mode ~= MODE_CONTACTS)) "With what?";
    if (player in parent(britney)) print_ret "She's standing right in front of me!";
    smartphone.interact();
    call_room = parent(britney);
    call_person = britney;
    #Ifdef ADDITIONAL_DEBUGGING;
    OutputCallInfo();
    #Endif;
    give britney concealed;
    move britney to location;
    move b_tits to location;
    remove self;
    print_ret (BritneysVoice)"Hey Kaitlyn, what's up?";
  ];

Room_Key b_key "Britney's key" britney
 class Britney_Object Ownable_Object
 with
  name 'Britney^s' 'room' 'key',
  article "Britney's",
  owner britney,
  key_to_room room_403
 has proper;

! Clothes that Britney's wearing
Top blue_shirt "blue t-shirt" britney
 with
  name 'blue' 'shirt' 'top' 't-shirt',
  description "It's a blue shirt.",
  article "Britney's",
  owner britney
 has worn;

Pants yoga_pants "yoga pants" britney
 with
  name 'yoga' 'pants',
  description "They're Britney's yoga pants.",
  article "Britney's",
 has worn pluralname;

! Clothes that are initially hidden
Lingerie b_bra "bra" thedark
 with
  name 'Britney^s' 'bra',
  description "It's a really sexy bra.",
  article "Britney's",
 has ~worn;

! Igor has the master key
Disappearing_Object master_key "master key" igor
 with
  name 'master' 'key',
  description "It has the hotel's logo, but no room number.",
  article "Igor's";

! Objects in hotel rooms
Top pink_shirt "pink shirt" room_402
 with
  name 'pink' 'shirt' 'top' 't-shirt',
  description "It's that pink shirt I love so much.",
  article "my",
  owner kaitlyn
 has ~worn;

Top green_shirt "green t-shirt" room_403
 with
  name 'green' 'shirt' 'top' 't-shirt',
  description "It Brit's green shirt.",
  article "Britney's"
  owner britney
 has ~worn;

Object fluffy "Fluffy" thedark;

! SMS thread with Vladimir Putkin
Guy anonymous "Anonymous" thedark
 with
  name 'anonymous';
